plugins {
    id "com.jfrog.bintray" version "1.8.1"
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.davidmedenjak.auth'
version = '0.2.1'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

bintray {
    user = 'bleeding182'
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'bleeding182'
        name = 'auth'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bleeding182/auth'
        version {
            name = project.version
            desc = ''
            vcsTag = System.getenv('TRAVIS_TAG')
        }
    }
    configurations = ['archives']
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives generateSourcesJar, generateJavadocsJar
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compileOnly 'com.android.support:support-annotations:27.1.1'

    compileOnly 'javax.inject:javax.inject:1'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.2"
    testImplementation "com.google.truth:truth:0.39"
    testImplementation "org.mockito:mockito-core:2.18.3"
}
