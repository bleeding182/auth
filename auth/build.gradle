apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.davidmedenjak.auth'
version = '0.3.0-SNAPSHOT'

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 32
    }

    buildFeatures {
        buildConfig = false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                artifactId = 'auth'
                pom {
                    name = "Auth"
                    description = "Auth library that wraps the AccountManager Framework"
                    url = "https://github.com/bleeding182/auth"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/bleeding182/auth/blob/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "bleeding182"
                            name = "David Medenjak"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/bleeding182/auth"
                        url = "https://github.com/bleeding182/auth"
                    }
                }
            }
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives generateSourcesJar, generateJavadocsJar
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.0.1'

    compileOnly 'javax.inject:javax.inject:1'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.8.1"
    testImplementation "com.google.truth:truth:0.39"
    testImplementation "org.mockito:mockito-core:2.18.3"
}
